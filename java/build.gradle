plugins {
    id 'java'
    id 'application'
    // https://github.com/johnrengelman/shadow
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    // https://plugins.gradle.org/plugin/com.google.protobuf
    id 'com.google.protobuf' version '0.8.19'
    //id 'idea' // use the idea plugin if using Jetbrains IntelliJ
}

group = 'com.ibm.aspera'
version = "0.1"

// if compilation lead to missing symbol:
// check that gRPC and protoc versions are compatible
// check on https://github.com/grpc/grpc-java/tree/v1.65.x
def grpcVersion = '1.65.1'
def protocVersion = '3.25.3'

// variables that can be set with -Pproto_dir=... -Pproto_file=...
def proto_dir = project.hasProperty('proto_dir') ? project.getProperty('proto_dir') : "$projectDir/build/resources/main"
def proto_file = project.hasProperty('proto_file') ? project.getProperty('proto_file') : 'transfer.proto'

def javaVersion = 23

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

tasks.withType(JavaCompile) {
    options.release = javaVersion
}

application {
  mainClass = 'COSFileUploadExample'
}

jar {
  manifest {
    attributes 'Main-Class': application.mainClass
  }
}

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

sourceSets {
    main {
        proto {
            srcDir proto_dir
            include proto_file
            exclude '*/**/*.proto'
        }
    }
}

// build jar with -all ext with all dependencies in it
shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
    // For issue: NameResolver 'unix' for '...' not supported by transport
    // https://github.com/grpc/grpc-java/issues/10853
    mergeServiceFiles() 
}

dependencies {
    compileOnly    'org.apache.tomcat:annotations-api:6.0.53'
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'org.json:json:20211205'
    implementation 'org.yaml:snakeyaml:1.30'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-gson:0.11.5'
    // http://kong.github.io/unirest-java/
    implementation 'com.konghq:unirest-java:3.14.2'
}
